#@gmic
#
#  File        : afre.gmic
#                ( G'MIC commands file )
#
#  Description : G'MIC commands and filters by afre.
#
#  Copyright   : Alan Kwan
#                ( https://discuss.pixls.us/u/afre )
#
#  License     : CeCILL v2.0
#                ( http://cecill.info/licences/Licence_CeCILL_V2-en.html )
#
#  This software is governed by the CeCILL  license under French law and
#  abiding by the rules of distribution of free software.  You can  use,
#  modify and/ or redistribute the software under the terms of the CeCILL
#  license as circulated by CEA, CNRS and INRIA at the following URL
#  "http://cecill.info".
#
#  As a counterpart to the access to the source code and  rights to copy,
#  modify and redistribute granted by the license, users are provided only
#  with a limited warranty  and the software's author,  the holder of the
#  economic rights,  and the successive licensors  have only  limited
#  liability.
#
#  In this respect, the user's attention is drawn to the risks associated
#  with loading,  using,  modifying and/or developing or reproducing the
#  software by the user in light of its specific status of free software,
#  that may mean  that it is complicated to manipulate,  and  that  also
#  therefore means  that it is reserved for developers  and  experienced
#  professionals having in-depth computer knowledge. Users are therefore
#  encouraged to load and test the software's suitability as regards their
#  requirements in conditions enabling the security of their systems and/or
#  data to be ensured and,  more generally, to use and operate it in the
#  same conditions as regards security.
#
#  The fact that you are presently reading this means that you have had
#  knowledge of the CeCILL license and that you accept its terms.
#

#@gui _<b>Colors</b>
#-------------------

#@gui Contrast : afre_contrast,afre_contrast_preview(1)+
#@gui : note = note("<strong>Enhance contrast with standard deviation.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2020Jul21.</strong>\n\n")
#@gui : SD &Delta;% = int(0,-10,10)
#@gui : Method = choice("Fast &#40;approx&#41;","Slow &#40;accurate&#41;")
#@cli afre_contrast : -10<=sd_change%<=10,_method={ 0=fast_approx | 1=slow_accurate }
#@cli : Enhance contrast with standard deviation.
#@cli : Default values: 'sd_change%=0' and 'method=0'.
afre_contrast : check "${1=0}>=-10 && isbool(${2=0}) && $1<=10"
  m=${arg\ 1+$2,fast,slow} e[] "[afre]^_^) contrast: Enhance contrast of image$? using target_sd%=$1 and method="$m"." if !$1 return fi
  repeat $! l[$>] r={[im,iM]} +l if !$2 r2dx 25% fi
    norm n 0,1 search_dichotomic "
      +f if(i<ia,ia*(i/ia)^$""1,1-(1-ia)*((1-i)/(1-ia))^$""1)
      u {sqrt(iv)} rm.",{$1/100+sqrt(iv)} rm endl
    if ${}=='nan' error[] "\n[afre]>_<) contrast: Choose a 'target_sd%' closer to '0'."
      else n 0,1 f if(i<ia,ia*(i/ia)^${},1-(1-ia)*((1-i)/(1-ia))^${})
      n $r fi
  endl done

afre_contrast_preview :
  afre_contrast $*

#@gui Local Contrast : afre_localcontrast,afre_localcontrast_preview(1)+
#@gui : note = note("<strong>Enhance local contrast.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2020Jul28.</strong>\n\n")
#@gui : Radius = int(5,1,10)
#@gui : Amount = int(50,-100,100)
#@cli afre_localcontrast : 1<=radius<=10,-100<=_amount<=100
#@cli : Enhance local contrast.
#@cli : Default values: 'radius=5' and 'amount=50'.
afre_localcontrast : check "${1=5}>=1 && ${2=50}>=-100 && $1<=10 && $2<=100"
  if $2==0 return fi e[] "[afre]^_^) localcontrast: Enhance local contrast of image$? using radius=$1 and amount=$2."
  repeat $! l[$>] r={[im,iM]} n 0,255 rgb2lab 0 sh 0 l.
    +f "begin(const A=2^($2/100); const boundary=1; const L=$1*2+1);
      ref(crop(x-$1,y-$1,0,c,L,L,1,1),N); M=avg(N);
      if(i<M,M*(i/M)^A,1-(1-M)*((1-i)/(1-M))^A)" c. 0,255
      +l.. norm n 0,1 f gauss(i-1/6) n 0,1 endl blend_fade[0,1] . k[0]
  endl rm. lab2rgb 0 n $r endl done

afre_localcontrast_preview :
  afre_localcontrast $*

#@gui Dark Sky : afre_darksky,afre_darksky_preview(1)+
#@gui : note = note("<strong>Enhance landscape by darkening the sky.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2017-2020Jul21.</strong>\n\n")
#@gui : note = note("<strong>Darken</strong>")
#@gui : Blend = choice("Softlight","Overlay")
#@gui : Contrast &Delta;% = int(0,-10,10)
#@gui : note = note("\n<strong>Smooth*</strong>")
#@gui : Method = choice(1,"Fast &#40;approx.&#41;","Slow &#40;accurate&#41;")
#@gui : Radius = int(0,0,3)
#@gui : note = note("\n<strong>Colour</strong>")
#@gui : Channels = choice(1,"RGB","Lightness &#40;CIELAB&#41;")
#@gui : note = note("\n* Preview is inaccurate.\n<code>Smooth</code> may appear to cause artifacts or affect edges more than it actually does in processing.")
#@cli afre_darksky : blend={ 0=softlight | 1=overlay },-10<=_contrast<=10,_method={ 0=fast_approx | 1=slow_accurate },0<=_radius<=3,_channels={ 0=RGB | 1=CIELAB_L }
#@cli : Enhance landscape by darkening the sky.
#@cli : Default values: 'blend=0', 'contrast=0', 'method=0', 'radius=0' and 'channels=1'.
afre_darksky : check "isbool(${1=0}) && ${2=0}>=-10 && isbool(${3=0}) && ${4=0}>=0 && isbool(${5=1}) && $2<=10 && $4<=3"
  b=${arg\ 1+$1,softlight,overlay} m=${arg\ 1+$3,fast,slow} c=${arg\ 1+$5,RGB,CIELAB_L} e[] "[afre]^_^) darksky: Darken sky of image$? using blend="$b", contrast=$2, method="$m", radius=$4 and channels="$c"."
  repeat $! l[$>] r0={[im,iM]} n 0,255 if s<3 error[] "\n[afre]>_<) darksky: Select RGB images." fi
    +l +channels 2 channels.. 1 - r1={[im,iM]}
      if $3&&$4 afre_gui0 $4,1e0 else median {$4*2+1} n $r1 fi
      c 0%,0 + 127 afre_contrast {$2}
    endl if $5 +blend $b rm.. blend lightness else blend $b fi
  n $r0 endl done

afre_darksky_preview :
  afre_darksky $*


#@gui _<b>Contours</b>
#---------------------

#@gui Edge : afre_edge, afre_edge_preview(0)+
#@gui : note = note("<strong>Compute edge.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019-2020May3.</strong>\n\n")
#@gui : Method = choice(0,"Gradient","Standard Deviation")
#@gui : Thinning = int(1,1,10)
#@gui : Recovery = float(1,.5,4)
#@gui : Brightness = float(1,.5,4)
#@gui : Details = float(1,.5,4)
#@cli afre_edge : method={ 0=gradient | 1=standard_deviation },1<=thinning<=10,0.5<=_recovery<=4,0.5<=_brightness<=4,0.5<=_details<=4
#@cli : Compute edge of selected images.
#@cli : Default values: 'method=0', 'thinning=1', 'recovery=1', 'brightness=1' and 'details=1'.
afre_edge : check "${2=1}>=1 && ${3=1}>=0.5 && ${4=1}>=0.5 && ${5=1}>=0.5 &&
    $2<=10 && $3<=4 && $4<=4 && $5<=4"
  e[] "[afre]^_^) edge: Compute edge of image$? using method=$1, thinning=$2, recovery=$3, brightness=$4 and details=$5."
  repeat $! l[$>] if $1 m "method : afre_sdpatch 1" else m "method : afre_gnorm" fi
    method im,ic,iM={[im,ic,iM]} ^ {1/$5} n $im,$iM
    repeat $2 +afre_box 1 method. r={[im,iM]} ^. $3 n. $r - done
    c $ic,$iM ^ {1/$4} n 0,$iM
  endl done um method

afre_edge_preview :
  afre_edge $*


#@gui _<b>Repair</b>
#-------------------

#@gui Clean Text : afre_cleantext, afre_cleantext_preview(0)+
#@gui : note = note("<strong>Clean scanned text.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019-Jun8.</strong>\n\n")
#@gui : Clean = int(8,0,10)
#@gui : Range = float(1,.2,1)
#@gui : Black = int(80,0,100)
#@gui : White = int(95,0,100)
#@cli afre_cleantext : 0<=clean<=10,0.2<=_range<=1,0<=_black<=100,0<=_white<=100
#@cli : Clean scanned text.
#@cli : Default values: 'clean=8', 'range=1', 'black=80' and 'white=95'.
afre_cleantext : check "${1=8}>=0 && ${2=1}>=0.2 && ${3=80}>=0 && ${4=95}>=0 &&
    $1<=10 && $2<=1 && $3<=100 && $4<=100"
  e[] "[afre]^_^) cleantext: Clean scanned text using clean=$1, range=$2, black=$3 and white=$4."
  c={(11-$1)*2+1} +dilate $c erode. $c - n 0,255
  retinex , norm n 0,1 f 1-gauss(i,$2)
  if $3!=$4 c $3%,$4% else c $3%,{$4+.5}% fi n 0,255 round

afre_cleantext_preview :
  afre_cleantext $*


#@gui _<b>Testing</b>
#--------------------
#@gui afre

#@gui Vigrect : afre_vigrect, afre_vigrect_preview(0)+
#@gui : note = note("<strong>Add rectangular vignette.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2017-2019.</strong>\n<hr />
#@gui : <strong>Instructions:</strong>\n\n
#@gui : * Negative strength brightens the periphery.\n\n")
#@gui : Size = int(50,1,100)
#@gui : Strength = int(75,-500,500)
#@gui : Blur = int(10,1,50)
#@gui : Left-Right = int(50,0,100)
#@gui : Up-Down = int(50,0,100)
#@cli afre_vigrect : 1<=size<=100,-500<=_strength<=500,1<=_blur<=50,0<=_position_x<=100,0<=_position_y<=100
#@cli : Add rectangular vignette of selected images. Negative strength will brighten periphery.
#@cli : Default values: 'size=50', 'strength=75', 'blur=10', 'position_x=50' and 'position_y=50'.
afre_vigrect : check "${1=50}>=1 && ${2=75}>=-500 && ${3=10}>=1 && ${4=50}>=0 && ${5=50}>=0 &&
    $1<=100 && $2<=500 && $3<=50 && $4<=100 && $5<=100"
  e[] "[afre]^_^) vigrect: Add rectangular vignette of image$? using size=$1, strength=$2, blur=$3, position_x=$4 and position_y=$5."
  repeat $! l[$>] iM={iM} min={min(w,h)} s={$min/100*(50.5-$1/2)}
    tx={$min/100*($4-50)} ty={$min/100*($5-50)}
    +f x<$s+$tx||x>w-$s+$tx||y<$s+$ty||y>h-$s+$ty
    distance. 0 b. {$min/100*$3} -. {im} *. {$2/100} -
  c 0,$iM endl done

afre_vigrect_preview :
  afre_vigrect $*

#@gui Vigcirc : afre_vigcirc, afre_vigcirc_preview(0)+
#@gui : note = note("<strong>Add circular vignette.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019.</strong>\n<hr />
#@gui : <strong>Instructions:</strong>\n\n
#@gui : * Negative strength brightens the periphery.\n\n")
#@gui : Size = int(90,50,150)
#@gui : Strength = int(75,-500,500)
#@gui : Left-Right = int(50,0,100)
#@gui : Up-Down = int(50,0,100)
#@cli afre_vigcirc : 50<=size<=150,-500<=_strength<=500,0<=_position_x<=100,0<=_position_y<=100
#@cli : Add circular vignette of selected images. Negative strength will brighten periphery.
#@cli : Default values: 'size=90', 'strength=75', 'position_x=50' and 'position_y=50'.
afre_vigcirc : check "${1=90}>=50 && ${2=75}>=-500 && ${3=50}>=0 && ${4=50}>=0 &&
    $1<=150 && $2<=500 && $3<=100 && $4<=100"
  e[] "[afre]^_^) vigcirc: Add circular vignette of image$? using size=$1, strength=$2, position_x=$3 and position_y=$4."
  repeat $! l[$>] iM={iM} min={min(w,h)}
    +f 1 circle. $3%,$4%,{($min/2-2)/100*$1}
    distance. 0 b. 1 -. {im} *. {$2/100} -
  c 0,$iM endl done

afre_vigcirc_preview :
  afre_vigcirc $*

#@gui Softlight : afre_softlight, afre_softlight_preview(0)+
#@gui : note = note{"<strong>Blend 1-2 images using softlight.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019-2020Jan21</strong>.\n<hr />
#@gui : <strong>Instructions</strong>&nbsp; The layers must be the same size.\n\n
#@gui : <strong>A. Self blend</strong>\n
#@gui : In the image editor, the active layer will be the base layer. Set <strong><i>Input layers</i></strong>&nbsp; to <strong><i>Active (default)</i></strong>.\n\n
#@gui : <strong>B. Two image blend</strong>\n
#@gui : In the image editor, the active layer will be the base layer. The layer directly above will be the blending layer. Set <strong><i>Input layers</i></strong>&nbsp; to <strong><i>Active and above</i></strong>.\n\n
#@gui : <strong>C. Optional</strong>\n
#@gui : Set <strong><i>Output mode</i></strong>&nbsp; to <strong><i>New layer(s)</i></strong>. This will allow you to output to a new layer, allowing you to preserve the input layers in case you would like to undo and redo the filter.\n\n"}
#@gui : Amount = int(100,-150,150)
#@gui : Reverse Order = bool(0)
#@cli afre_softlight : -150<=amount<=150,_reverse_order={ 0 | 1 }
#@cli : Blend 1-2 images using softlight.
#@cli : Default values: 'amount=100' and 'reverse_order=0'.
afre_softlight : check "${1=100}>=-150 && isbool(${2=0}) && $1<=150"
  if $1<0 i=" inverse" fi
  if $!>2 error[] "\n[afre]>_<) softlight: Please select 1 or 2 images."
  elif $!>1 if w#0!=w#1||h#0!=h#1 error[] "\n[afre]>_<) softlight: Please select images with the same width and height." fi
    if $2 rv=" in reverse order" fi e[] "[afre]^_^) softlight: Blend image$?"${rv}" using"${i}" softlight."
  else e[] "[afre]^_^) softlight: Blend image$? with itself using"${i}" softlight." .
  fi to_colormode 0 a={.5*abs($1)/100+.5}
  if $2 rv fi nm={0,b} r={0,[im,iM]} n 0,1 if $1<0 negate. fi
  *. -2 +. 1 *. $a f. i#0^((2*$a)^i#1) k. n $r nm $nm

afre_softlight_preview :
  afre_softlight $*

#@gui Sharpen FFT : afre_sharpenfft, afre_sharpenfft_preview(0)+
#@gui : note = note("<strong>Sharpen with Fourier transform.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019-2020Jan18.</strong>\n\n")
#@gui : Strength = int(15,1,50)
#@gui : Size = int(1,1,10)
#@cli afre_sharpenfft : 1<=strength<=50,1<=_size<=10
#@cli : Sharpen selected images with Fourier transform.
#@cli : Default values: 'strength=15' and 'size=1'.
afre_sharpenfft : check "${1=15}>=1 && ${2=1}>=1 && $1<=50 && $2<=10"
  e[] "[afre]^_^) sharpenfft: Sharpen image$? with Fourier transform using strength=$1 and size=$2."
  repeat $! l[$>] iM={iM} +periodize_poisson -.. . l. +l
    fftpolar +gaussian.. {100/$2}% negate. *[^1] ifftpolar * $1
  endl + c 0,$iM endl + c 0,$iM endl done

afre_sharpenfft_preview :
  afre_sharpenfft $*

#@gui Contrast FFT : afre_contrastfft, afre_contrastfft_preview(0)+
#@gui : note = note("<strong>Enhance contrast with Fourier transform.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019.</strong>\n\n")
#@gui : Strength = int(75,1,100)
#@gui : Amount = int(50,0,150)
#@gui : Iterations = int(1,1,10)
#@cli afre_contrastfft : 1<=strength<=100,0<=_amount<=150,1<=_iterations<=10
#@cli : Enhance contrast for selected images with Fourier transform.
#@cli : Default values: 'strength=75', 'amount=50' and 'iterations=1'.
afre_contrastfft : check "${1=75}>=1 && ${2=50}>=0 && ${3=1}>=1 &&
    $1<=100 && $2<=150 && $3<=10"
  e[] "[afre]^_^) contrastfft: Enhance contrast for image$? with Fourier transform using strength=$1, amount=$2 and iterations=$3."
  repeat $! l[$>] repeat $3
    +fftpolar ^.. {($1-.5)/100} ifftpolar[^0] afre_softlight $2
  done endl done

afre_contrastfft_preview :
  afre_contrastfft $*

#@gui Gamify : fx_gamify, fx_gamify_preview(0)+
#@gui : Lightness = int(50,50,60)
#@gui : Chroma = float(2,1.1,5)
#@gui : sep = separator(), Normalize = bool(1)
#@gui : Contrast = float(1,1,1.5)
#@gui : sep = separator(), Preview type = choice("Full","Forward horizontal","Forward vertical","Backward horizontal","Backward vertical","Duplicate top","Duplicate left","Duplicate bottom","Duplicate right","Duplicate horizontal","Duplicate vertical","Checkered","Checkered inverse")
#@gui : sep = separator(), note = note("Filter by <i>afre</i>. Latest update: <i>2017-01-05</i>.")
fx_gamify :
  if $3 n 0,255 fi
  rgb2lab lab2lch
  s c n... $1,100 *.. $2 a c
  lch2lab lab2rgb
  split_details 2
  l.. adjust_colors 0,{-$4/1.5*20} endl
  *. $4
  + c 0,255

fx_gamify_preview :
  gui_split_preview "fx_gamify $*",$-1

#@gui Hessian Norm : fx_hnorm, fx_hnorm_preview(0)+
#@gui : Strength = float(1,.5,1.5)
#@gui : Contrast = int(50,1,99)
#@gui : Invert = bool(0)
#@gui : sep = separator(), note = note("Filter by <i><a href="https://discuss.pixls.us/u/afre">afre</a></i>. Latest update: <i>2018-05-09</i>.")
fx_hnorm :
  afre_hnorm ^ $1
  c 0,$2%
  if $3 negate fi
  n 0,255

fx_hnorm_preview :
  fx_hnorm $*

#@gui Gleam : afre_gleam, afre_gleam_preview(0)+
#@gui : note = note("<strong>Add gleam effect.</strong>&nbsp; Hint: Try different combinations of smooth and threshold.&nbsp; <strong>Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2019.</strong>\n\n
#@gui : * Experimental filter.\n\n")
#@gui : Smooth = int(3,2,100)
#@gui : Threshold = int(50,10,90)
#@cli afre_gleam : 2<=smooth<=100,10<=_threshold<=90
#@cli : Add gleam effect to selected images.
#@cli : Default values: 'smooth=3' and 'threshold=50'.
afre_gleam : check "${1=3}>=2 && ${2=50}>=10 && $1<=100 && $2<=90"
  e[] "[afre]^_^) gleam: Add gleam effect to image$? using smooth=$1 and threshold=$2."
  repeat $! l[$>]
    +afre_y50 1 >. {100-$2}% b. $1% sqrt. +afre_softlight 100 rm.. max
  endl done

afre_gleam_preview :
  afre_gleam $*

#@gui Half & half : afre_halfhalf, afre_halfhalf_preview(0)+
#@gui : note = note("<strong>Stitch left and right halves of 2 images, respectively.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2020-Feb21.</strong>\n\n
#@gui : * Remember to set <strong>Input layers</strong>.\n
#@gui : * <strong>Match size</strong> doesn't work in Krita.\n\n")
#@gui : Match size = bool(0)
#@cli afre_halfhalf : match_size={ 0 | 1 }
#@cli : Stitch left and right halves of 2 images, respectively.
#@cli : Default value: 'match_size=0'.
afre_halfhalf : check isbool(${1=0})
  if $!!=2 error[] "\n[afre]>_<) halfhalf: Please select 2 images." fi
  e[] "[afre]^_^) halfhalf: Stitch left and right halves of 2 images using match_size=$1."
  +f[{w#0>w#1?0:1}] 0 mv. 0 s[1,2] x,2 rm[2,3]
  if w#1<w#2&&$1 r2dx.. {w#2}
    elif $1 r2dx. {w#1} elif w#1<w#2 r.. .,0,0,1,.5 fi
  j[0] [1],0,.5~ j[0] [2],50%,.5~ k[0]

afre_halfhalf_preview :
  afre_halfhalf $*

#@gui Portrait Montage : afre_portraitmontage, afre_portraitmontage_preview(0)+
#@gui : note = note("<strong>Generate portrait montage.&nbsp; Filter by <a href="https://discuss.pixls.us/u/afre">afre</a> 2020Jun28.</strong>\n\n
#@gui : - Set <strong>Input layers</strong> to <strong>All</strong>.\n
#@gui : - Portraits should be centred and have the same dimensions.\n\n")
#@gui : Spacing = int(5,0,10)
#@gui : Colour = color(230,255,230)
#@cli afre_portraitmontage : 0<=spacing<=10,0<=_colour={[R,G,B]}<=255
#@cli : Generate portrait montage.
#@cli : Default values: 'spacing=5' and 'colour=230,255,230'.\n
#@cli : Portraits should be centred and have the same dimensions.
afre_portraitmontage : check "${1=5}>=0 && ${2=230}>=0 && ${3=255}>=0 && ${4=230}>=0 && $1<=10"
  repeat $! l[$>] +f. 1 +circle. 50%,50%,{min(w,h)*.49} n[0,1] 0,255
  -[1] 1 blend_fade[0,1] . k[0] frame {[$1,$1]*min(w,h)*.01},-1 endl done
  append_tiles 0 replace_color 0,0,-1,-1,-1,$2,$3,$4

afre_portraitmontage_preview :
  afre_portraitmontage $*


#@cli Core Commands
#------------------

#@cli afre_gnorm
#@cli : Compute custom gradient norm of selected images.
afre_gnorm :
  e[] "[afre]^_^) gnorm: Compute custom gradient norm of image$?."
  repeat $! l[$>] nm={b}
    +gradient_norm r={[im,iM]} +afre_hnorm[0] afre_gui1[2] [0] rm[0]
    c. 20%,100% n 0,1 *. .75 max n $r
  nm $nm endl done

#@cli afre_hnorm
#@cli : Compute Hessian norm of selected images.
afre_hnorm :
  e[] "[afre]^_^) hnorm: Compute Hessian norm of image$?."
  repeat $! l[$>] hessian sqr s c + sqrt endl done

#@cli afre_sdpatch : radius>=1
#@cli : Compute local standard deviation of selected images.
#@cli : Default value: 'radius=1'.
afre_sdpatch : skip ${1=1}
  e[] "[afre]^_^) sdpatch: Compute local standard deviation of image$? using radius=$1."
  repeat $! l[$>] +gradient_norm r={[im,iM]} rm.
    +sqr afre_box $1 sqr.. rv - max 0 sqrt
  n $r endl done

#@cli afre_reorder : order,_order2,...
#@cli : Reposition images according to a list of values of any length.
afre_reorder :
  e[] "[afre]^_^) reorder: Reposition images according to list $*."
  z 0,0,0,100%,100%,1 mirror z f z==0?1e6:i
  l ($*) onfail error[] "\n[afre]>_<) reorder: Parameter must be a list of numbers; e.g. 0,2,4,1,3." endl
  repeat min(w,$!-1) f[$>] z==0?{i($>)}:i done
  sort_list[0-{min(w,$!-1)-1}] + rm. z 0,0,1,100%,100%,1

#@cli afre_section : 1<=xy_sections<=50
#@cli : Section selected images into numbered regions.
#@cli : Default value: 'xy_sections=20'.
afre_section : check "${1=20}>=1 && $1<=50"
  m=min(w,h) err=int($m/25) if $err<$1 error[] "\n[afre]>_<) section: Section image$? into no more than "{max(sqr($err),1)}" ("{max($err,1)}"x"{max($err,1)}") regions." fi
  e[] "[afre]^_^) section: Section image$? into "{sqr($1)}" ($1x$1) numbered regions."
  repeat $! l[$>]
    im,iM={[im,iM]} s yx,$1 repeat $!
      t[$>] {$>+1},.5~,.5~,{$m*.55},.9,$im t[$>] {$>+1},.5~,.5~,{$m*.56},.9,$iM
    done append_tiles $1
    grid {100/$1}%,{100/$1}%,,,.2,$im
    grid {100/$1}%,{100/$1}%,-1,-1,.2,$iM
  endl done

#@cli afre_cleanphoto : 2<=size<=10,1<=_recovery<=100,-50<=_xy_sections<=50,_maskN>=1,...
#@cli : Clean dust and scratches from photos.
#@cli : Default values: 'size=3', 'recovery=10', 'xy_sections=1' and 'maskN=1'.\n
#@cli : - 'xy_sections' sections image into MxM labeled regions.
#@cli : \ \ \ For global processing, set 'xy_sections' to 1.
#@cli : \ \ \ To visualize these regions with labels, use a negative number.
#@cli : - 'maskN' is a comma separated list of regions to be processed.
afre_cleanphoto : check "${1=3}>=2 && ${2=10}>=1 && ${3=1}>=-50 &&
    $1<=10 && $2<=100 && $3<=50" skip ${4=1}
  m=min(w,h) err=int($m/25) if $err<$3 error[] "\n[afre]>_<) cleanphoto: Section image$? into no more than "{max(sqr($err),1)}" ("{max($err,1)}"x"{max($err,1)}") regions." fi
  m "ip : p=$1,,,{iM}
    +erode_threshold $p dilate_threshold. $p afre_gui1. .. c. 0,100%
    +afre_edge.. 1,1,4,4 afre_box. 1 c. 0,$2% n. 0,1 +blend_fade[0,1] . k[0,-1]
    +gaussian. {[w,h]*.3} c. 0,50% n. 0,1 blend_fade[0,1] . k[0]"
  e[] "[afre]^_^) cleanphoto: Clean dust and scratches from image$? using size=$1, recovery=$2, xy_sections=$3 and masks=${4--1}."
  repeat $! l[$>] if $3>1
    iM={iM} w0,h0={[w,h]} w,h={[w,h]/$3} 100%,100%,1,3 l. (${4--1})
      c. 1,{sqr($3)} round. sort. discard. unroll. x
      repeat w v={i($>)} i={$v%$3?$v%$3:$3} j={($v+$3-$i)/$3}
        r={[$w*$i-$w,$h*$j-$h,$w*$i-1,$h*$j-1]} rectangle[0] $r,1,$iM
      done rm. label_fg 0 r {[$w0,$h0]+2},1,3,0,0,.5,.5
      repeat iM +==[0] {$>+1} coord=${"
        w={w} h={h} val={i(w-1,h-1)} +=. {1+$val},100%,100%,100%
        autocrop. =. $val,100%,100%,100% x0={$w-w} y0={$h-h} rm.
        +autocrop. x1={$x0+w-1} y1={$y0+h-1} rm. u $x0,$y0,$x1,$y1
      "} rm. ($coord) done
    a[^0] y endl r[0] {[$w0,$h0]+2},1,3,0,0,.5,.5
    repeat h x0,y0,x1,y1={@{[0,1,2,3]+4*$>}} +z[0,1] $x0,$y0,$x1,$y1
      +ip.. f. i#4?i#5:i#3 j[0] .,$x0,$y0 rm[-3--1]
    done k[0] r {[$w0,$h0]},1,3,0,0,.5,.5
  else ip
  fi endl done um ip

#@cli afre_cleanphoto1 : 1<=radius<=10,1<=_xy_sections<=50,_maskN>=1,...
#@cli : Clean dust and scratches from photos.
#@cli : Default values: 'radius=3', 'xy_sections=1' and 'maskN=1'.\n
#@cli : - 'xy_sections' sections image into MxM labeled regions.
#@cli : \ \ For global processing, set 'xy_sections' to 1.
#@cli : - 'maskN' is a comma separated list of regions to be processed.
#@cli : \ \ Use 'afre_section' to determine region labels.
afre_cleanphoto1 : check "${1=3}>=1 && ${2=1}>=1 &&
    $1<=10 && $2<=50" skip ${3=1}
  m=min(w,h) err=int($m/25) if $err<$2 error[] "\n[afre]>_<) cleanphoto: Section image$? into no more than "{max(sqr($err),1)}" ("{max($err,1)}"x"{max($err,1)}") regions." fi
  m "ip : s={$1*2+1} +l +erode_oct $s dilate_oct. $s - c 0,100% ^ 2
    c 5%,10% dilate_oct 3 n 0,1 endl +inpaint_pde.. . rm..
    +gaussian. {[w,h]*.3} c. 0,50% n. 0,1 blend_fade[0,1] . k[0]"
  e[] "[afre]^_^) cleanphoto: Clean dust and scratches from image$? using radius=$1, xy_sections=$2 and maskN=${3--1}."
  repeat $! l[$>] if $2>1
    iM={iM} w0,h0={[w,h]} w,h={[w,h]/$2} 100%,100%,1,3 l. (${3--1})
      c. 1,{sqr($2)} round. sort. discard. unroll. x
      repeat w v={i($>)} i={$v%$2?$v%$2:$2} j={($v+$2-$i)/$2}
        r={[$w*$i-$w,$h*$j-$h,$w*$i-1,$h*$j-1]} rectangle[0] $r,1,$iM
      done rm. label_fg 0 r {[$w0,$h0]+2},1,3,0,0,.5,.5
      repeat iM +==[0] {$>+1} coord=${"
        w={w} h={h} val={i(w-1,h-1)} +=. {1+$val},100%,100%,100%
        autocrop. =. $val,100%,100%,100% x0={$w-w} y0={$h-h} rm.
        +autocrop. x1={$x0+w-1} y1={$y0+h-1} rm. u $x0,$y0,$x1,$y1
      "} rm. ($coord) done
    a[^0] y endl r[0] {[$w0,$h0]+2},1,3,0,0,.5,.5
    repeat h x0,y0,x1,y1={@{[0,1,2,3]+4*$>}} +z[0,1] $x0,$y0,$x1,$y1
      +ip.. f. i#4?i#5:i#3 j[0] .,$x0,$y0 rm[-3--1]
    done k[0] r {[$w0,$h0]},1,3,0,0,.5,.5
  else ip
  fi endl done um ip

#@cli afre_compare : crop={ 1=once | 2=twice },_layout={ x | y | z | tile },_normalize={ 0 | 1 }
#@cli : Crop then compare selected images interactively.
#@cli : Default values: 'crop=1', 'layout=x' and 'normalize=1'.\n
#@cli : 1. Interactive Crop
#@cli : \ - Click and drag to select crop.
#@cli : \ - Use scroll button to browse images.
#@cli : \ * Click once or close window to exit command.\n
#@cli : 2. Comparison Window
#@cli : \ - Regular display behaviour.
#@cli : \ - Close window to make another comparison.
afre_compare : check "${1=1}>=1 && $1<=2 && (s='${2=x}';s=='x'||s=='y'||s=='z'||s=='tile') && "isbool(${3=1})
  n={$!-1} if $n<1 error[] "\n[afre]>_<) compare: Please select 2 or more images." fi
  repeat $!-1 if w#0!=w#{$>+1}||h#0!=h#{$>+1} error[] "\n[afre]>_<) compare: Please select images with the same width and height." fi done
  repeat $! if d#$>!=1 e[] "[afre]>_<) compare: *Slices in image ["$>"] will be considered separate images." fi done
  e[] "[afre]^_^) compare: Crop then compare image$? interactively."
  e[] "\n 1. Interactive Crop\n
    \ - Click and drag to select crop.\n
    \ - Use scroll button to browse images.\n
    \ * Click once or close window to exit command.\n\n
    2. Comparison Window\n
    \ - Regular display behaviour.\n
    \ - Close window to make another comparison.\n"
  to_colormode 0 if $3 n 0,255 fi
  do +l nm[0] Compare
    a z repeat $1 xz b={w} if $b==1 break fi done
    b={w} if '$2'!='z' s z if '$2'!='tile' a $2 else append_tiles 0 fi fi
    if $b==1 break else v - d v + fi
  endl k[0-$n] while $b!=1 v - d[]

#@cli afre_log2 : direction={ 0=inverse | 1=forward }
#@cli : Transform selected images into or back from log2 space.
#@cli : Default value: 'direction=1'.
afre_log2 : check isbool(${1=1})
  if $1 d="into" else d="back from" fi
  e[] "[afre]^_^) log2: Transform image$? "$d" log2 space."
  if $1 + 1 log2 else f exp(i*log(2)) - 1 fi

#@cli afre_y50 : colour_space={ 0=Rec.709 | 1=Rec.2020 }
#@cli : Compute luminance (D50) of selected images.
#@cli : Default value: 'colour_space=1'.
afre_y50 : check isbool(${1=1})
  if $1 Y="Rec.2020" else Y="Rec.709" fi
  e[] "[afre]^_^) y50: Compute luminance (D50 "$Y") of image$?."
  repeat $! l[$>]
    if s==3 sh 0 sh[0] 1 sh[0] 2
      if $1 *[1] 0.27904 *[2] 0.67535 *[3] 0.04561         # Rec.2020
      else *[1] 0.22248840 *[2] 0.71690369 *[3] 0.06060791 # Rec.709
      fi +[1-3] rm[1] channels 0
    elif s!=1 r={[im,iM]} norm n $r fi
  endl done

#@cli afre_orien : colour_space={ 0=Rec.709 | 1=Rec.2020 }
#@cli : Compute orientation of selected images using luminance (D50).
#@cli : Default value: 'colour_space=1'.
afre_orien : check isbool(${1=1})
  if $1 Y="Rec.2020" else Y="Rec.709" fi
  e[] "[afre]^_^) orien: Compute orientation of image$? using luminance (D50 "$Y")."
  repeat $! +afre_y50[$>] $1 replace. 0,1 /[$>,-1] done

#@cli afre_conv : [kernel]
#@cli : Convolve selected images with a custom kernel.
afre_conv : check ${is_image_arg\ $1}
  repeat $! pass$1 0
    e[] "[afre]^_^) conv: Convolve image$? using ["${"-pass$1 -1"}"]."
    l[$>,-1] +f[0] 1 mv. 1
    +rows. 50% columns.. 50% +convolve[1] [2] convolve[4] [3]
    convolve[0] [2] convolve[0] [3] k[0,4] / endl
  done

#@cli afre_box : radius>=1,_gaussian={ 0 | 1 }
#@cli : Blur selected images with a soft box filter.
#@cli : Default values: 'radius=1' and 'gaussian=1'.
afre_box : check "${1=1}>=1 && isbool(${2=1})"
  if $2 g="soft box" else g="box" fi w={$1*2+1}
  e[] "[afre]^_^) box: Blur image$? with a "${w}"x"${w}" "$g" filter."
  repeat $! l[$>]
    $w,$w f. 1 if $2 gaussian. {100/3}% fi afre_conv.. . rm.
  endl done

#@cli afre_gui0 : radius>=1,_smoothing>=0,0<=_structure<=10
#@cli : Blur selected images with a custom self-guided filter.
#@cli : Default values: 'radius=1', 'smoothing=1e-2' and 'structure=10'.\n
#@cli : - Use orders of magnitude for 'smoothing'.
#@cli : - Reduce 'structure' to soften edges and artifacts.
#@cli : * Improvement of "K He, J Sun, X Tang. Guided image filtering. TPAMI, 35(6):1397-1409, 2013".
afre_gui0 : check "${1=1}>=1 && ${2=1e-2}>=0 && ${3=10}>=0 && $3<=10"
  e[] "[afre]^_^) gui0: Blur image$? with a custom self-guided filter using radius=$1, smoothing=$2 and structure=$3."
  kwh={sqr($1*2+1)} str={1e{-$3+2}}
  repeat $! l[$>] nm={b} iM={iM} / $iM
    . +sqr. afre_box[^0] $1 nm I,mI,mII
    +sqr[mI] *. -1 +. [mII] rm[mII] nm. vI
    +l[vI] abs s c + endl *. $kwh +. $str ^. -1 *. $str nm. W
    ++[vI] {vI,$2*ia+1e-6} /[vI] . rm. nm[vI] a
    +*[a] [mI] *. -1 +. [mI] rm[mI] nm. b
    *[a,b] [W] afre_box[^0] $1 nm[^0] ma,mW,mb
    *[I] [ma] +[I] [mb] /[I] [mW] k[I]
  * $iM nm $nm endl done

#@cli afre_gui1 : [guide],_radius>=1,_smoothing>=0,0<=_structure<=10
#@cli : Blur selected images with a custom guided filter.
#@cli : Default values: 'radius=1', 'smoothing=1e-2' and 'structure=5'.\n
#@cli : - Use orders of magnitude for 'smoothing'.
#@cli : - Reduce 'structure' to soften edges and artifacts.
#@cli : * Improvement of "K He, J Sun, X Tang. Guided image filtering. TPAMI, 35(6):1397-1409, 2013".
afre_gui1 : check ${is_image_arg\ $1}" && ${2=1}>=1 && ${3=1e-2}>=0 && ${4=5}>=0 && $4<=10"
  e[] "[afre]^_^) gui1: Blur image$? with a custom guided filter using guide=["${"-pass$1 -1"}"], radius=$2, smoothing=$3 and structure=$4."
  pass$1 0 to_colormode 0 kwh={sqr($2*2+1)} str={1e{-$4+2}}
  repeat $! if $>!=$! nm$>={$>,b} fi iM$>={$>,iM} /[$>] ${iM$>} done
  repeat $!-1 l[$>,-1]
    if w#0!=w#1||h#0!=h#1||d#0!=d#1 error[] "\n[afre]>_<) gui1: Image$? and guide ["${"-pass$1 -1"}"] must have the same width, height and depth." fi
    . rv +*[1] [2] +sqr[1] afre_box[^0] $2 nm I,mI,mp,mIp,mII
    +*[mI] [mp] *. -1 +. [mIp] rm[mIp] nm. cIp
    +sqr[mI] *. -1 +. [mII] rm[mII] nm. vI
    +normp[vI] 1 *. $kwh +. $str ^. -1 *. $str nm. W
    ++[vI] {vI,$3*ia+1e-6} /[cIp] . rm[vI,-1] nm[cIp] a
    +*[a] [mI] *. -1 +. [mp] rm[mI,mp] nm. b
    *[a,b] [W] afre_box[^0] $2 nm[^0] ma,mW,mb
    *[ma] [I] +[ma] [mb] /[ma] [mW] k[I,ma]
    nm[ma] ${nm$>} rv
  endl done rm.
  repeat $! *[$>] ${iM$>} done

#@cli afre_gui0c : [kernel],_smoothing>=0,0<=_structure<=10
#@cli : Convolve selected images with a custom self-guided filter.
#@cli : Default values: 'smoothing=1e-2' and 'structure=10'.\n
#@cli : - Use orders of magnitude for 'smoothing'.
#@cli : - Reduce 'structure' to soften edges and artifacts.
#@cli : * Improvement of "K He, J Sun, X Tang. Guided image filtering. TPAMI, 35(6):1397-1409, 2013".
afre_gui0c : check ${is_image_arg\ $1}" && ${2=1e-2}>=0 && ${3=10}>=0 && $3<=10"
  e[] "[afre]^_^) gui0c: Convolve image$? with a custom self-guided filter using kernel=["${"-pass$1 -1"}"], smoothing=$2 and structure=$3."
  str={1e{-$3+2}}
  repeat $! pass$1 0 kwh={wh} l[$>,-1] nm={0,b} iM={0,iM} /[0] $iM
    [0] +sqr. afre_conv[^0-1] [1] nm I,k,mI,mII
    +sqr[mI] *. -1 +. [mII] rm[mII] nm. vI
    +l[vI] abs s c + endl *. $kwh +. $str ^. -1 *. $str nm. W
    ++[vI] {vI,$2*ia+1e-6} /[vI] . rm. nm[vI] a
    +*[a] [mI] *. -1 +. [mI] rm[mI] nm. b
    *[a,b] [W] afre_conv[^0-1] [k] nm[^0-1] ma,mW,mb
    *[I] [ma] +[I] [mb] /[I] [mW] k[I]
  * $iM nm $nm endl done

#@cli afre_gui1c : [guide],[kernel],_smoothing>=0,0<=_structure<=10
#@cli : Convolve selected images with a custom guided filter.
#@cli : Default values: 'smoothing=1e-2' and 'structure=5'.\n
#@cli : - Use orders of magnitude for 'smoothing'.
#@cli : - Reduce 'structure' to soften edges and artifacts.
#@cli : * Improvement of "K He, J Sun, X Tang. Guided image filtering. TPAMI, 35(6):1397-1409, 2013".
afre_gui1c : check ${is_image_arg\ $1}" && "${is_image_arg\ $2}" && ${3=1e-2}>=0 && ${4=5}>=0 && $4<=10"
  e[] "[afre]^_^) gui1c: Convolve image$? with a custom guided filter using guide=["${"-pass$1 -1"}"], kernel=["${"-pass$2 -1"}"], smoothing=$3 and structure=$4."
  pass$1 0 to_colormode 0 str={1e{-$4+2}}
  repeat $! if $>!=$! nm$>={$>,b} fi iM$>={$>,iM} /[$>] ${iM$>} done
  repeat $!-1 pass$2 0 kwh={wh} l[$>,-2--1]
    if w#0!=w#1||h#0!=h#1||d#0!=d#1 error[] "\n[afre]>_<) gui1c: Image$? and guide ["${"-pass$1 -1"}"] must have the same width, height and depth." fi
    [1] rv +*[2] [3] +sqr[2] afre_conv[^0-1] [1] nm I,k,mI,mp,mIp,mII
    +*[mI] [mp] *. -1 +. [mIp] rm[mIp] nm. cIp
    +sqr[mI] *. -1 +. [mII] rm[mII] nm. vI
    +normp[vI] 1 *. $kwh +. $str ^. -1 *. $str nm. W
    ++[vI] {vI,$3*ia+1e-6} /[cIp] . rm[vI,-1] nm[cIp] a
    +*[a] [mI] *. -1 +. [mp] rm[mI,mp] nm. b
    *[a,b] [W] afre_conv[^0-1] [k] nm[^0-1] ma,mW,mb
    *[ma] [I] +[ma] [mb] /[ma] [mW] k[I,ma]
    nm[ma] ${nm$>} rv
  endl done rm.
  repeat $! *[$>] ${iM$>} done


#@cli Support Commands
#---------------------

#@cli search_dichotomic : "increasing_fn",target_y,_precision>0
#@cli : Find parameter for function such that 'target_y' is met in image.
#@cli : Default value: 'precision=1e-3'.\n
#@cli : - Return 'nan' if search fails.
#@cli : * Credit: David Tschumperle.
search_dichotomic : check ${3=1e-3}>0
  m "_sdc : $1" target_value,epsilon=$2,$3 mpos,nb_attempts=-1,20
  do mvalue=${_sdc\ $mpos} if $mvalue<$target_value break fi mpos*=2
  nb_attempts-=1 while $nb_attempts if !$nb_attempts u nan return fi
  Mpos,nb_attempts=1,20
  do Mvalue=${_sdc\ $Mpos} if $Mvalue>$target_value break fi Mpos*=2
  nb_attempts-=1 while $nb_attempts if !$nb_attempts u nan return fi
  nb_attempts=100
  do
    cpos={($mpos+$Mpos)/2} cvalue=${_sdc\ $cpos}
    if abs($cvalue-$target_value)<$epsilon u $cpos return
      elif $cvalue<$target_value mpos=$cpos else Mpos=$cpos fi
    nb_attempts-=1
  while $nb_attempts u {$nb_attempts?$cpos:nan}


# List of Commands and Filters
#-----------------------------
# GUI CLI : afre_contrast afre_localcontrast afre_darksky afre_edge afre_cleantext afre_vigrect afre_vigcirc
#           afre_softlight afre_sharpenfft afre_contrastfft afre_gleam afre_halfhalf afre_portraitmontage
# CLI only: afre_gnorm afre_hnorm afre_sdpatch afre_reorder afre_section afre_cleanphoto afre_cleanphoto1 afre_compare
#           afre_log2 afre_y50 afre_orien afre_conv afre_box afre_gui0 afre_gui1 afre_gui0c afre_gui1c search_dichotomic
# GUI old : fx_gamify fx_hnorm


# Local Variables:
# mode: sh
# End:
#
# (End of G'MIC custom commands)
